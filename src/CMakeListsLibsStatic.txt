set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

if (WIN32)
    set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
endif()

# (1) Utils
#

add_library(
  nomadUtilsStatic
  STATIC
    ${MATH_SOURCES} 
    ${OUTPUT_SOURCES} 
    ${PARAM_SOURCES} 
    ${TYPE_SOURCES} 
    ${UTIL_SOURCES} 
    ${ATTRIBUTE_HEADERS}
)

target_include_directories(
  nomadUtilsStatic
  PUBLIC
    $<BUILD_INTERFACE: 
      ${CMAKE_SOURCE_DIR}/src/Attribute 
      ${CMAKE_SOURCE_DIR}/src/Math 
      ${CMAKE_SOURCE_DIR}/src/Output
      ${CMAKE_SOURCE_DIR}/src/Param 
      ${CMAKE_SOURCE_DIR}/src/Type 
      ${CMAKE_SOURCE_DIR}/src/Util
    >
    $<INSTALL_INTERFACE: 
      ${CMAKE_INSTALL_INCLUDEDIR}
    >
)

if(OpenMP_CXX_FOUND)
  target_link_libraries(
    nomadUtilsStatic
    PUBLIC 
      OpenMP::OpenMP_CXX
  )
endif()

set_target_properties(
  nomadUtilsStatic 
  PROPERTIES 
    VERSION "${NOMAD_VERSION}"
    SOVERSION "${NOMAD_VERSION_MAJOR}.${NOMAD_VERSION_MINOR}"
    OUTPUT_NAME nomadUtilsStatic
)

if (WIN32)
  target_compile_definitions(
    nomadUtilsStatic
    PRIVATE 
      -DNOMAD_BUILD_STATIC
  )
endif()

# (2) Eval
#

add_library(
  nomadEvalStatic
  STATIC
    ${CACHE_SOURCES} 
    ${EVAL_SOURCES}
)

target_include_directories(
  nomadEvalStatic
  PUBLIC
    $<BUILD_INTERFACE: 
      ${CMAKE_CURRENT_SOURCE_DIR}/Cache 
      ${CMAKE_CURRENT_SOURCE_DIR}/Eval
    >
    $<INSTALL_INTERFACE: 
      ${CMAKE_INSTALL_INCLUDEDIR}
    >
)

target_link_libraries(
  nomadEvalStatic
  PUBLIC 
    nomadUtilsStatic
)

if(OpenMP_CXX_FOUND)
  target_link_libraries(
    nomadEvalStatic
    PUBLIC 
      OpenMP::OpenMP_CXX
  )
endif()

set_target_properties(
  nomadEvalStatic
  PROPERTIES 
    VERSION "${NOMAD_VERSION}"
    SOVERSION "${NOMAD_VERSION_MAJOR}.${NOMAD_VERSION_MINOR}"
    OUTPUT_NAME nomadEvalStatic
)

if(WIN32)
  target_compile_definitions(
    nomadEvalStatic
    PRIVATE 
      -DNOMAD_BUILD_STATIC
  )
endif()

# (3) Algos
#

add_library (
  nomadAlgosStatic
  STATIC
    ${ALGOS_SOURCES} 
    ${CS_SOURCES} 
    ${DMULTIMADS_SOURCES} 
    ${MADS_SOURCES} 
    ${LH_SOURCES} 
    ${NM_SOURCES} 
    ${PHASEONE_SOURCES} 
    ${PSDMADS_SOURCES} 
    ${SSDMADS_SOURCES}
    ${TEMPLATE_ALGO_SOURCES} 
    ${VNSMADS_SOURCES}
)

target_include_directories(
  nomadAlgosStatic
  PUBLIC
    $<BUILD_INTERFACE: 
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/CoordinateSearch
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/DMultiMads
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/LatinHypercubeSampling
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/Mads
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/NelderMead
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/PhaseOne
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/PSDMads
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/SSDMads
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/TemplateAlgo
      ${CMAKE_CURRENT_SOURCE_DIR}/Algos/VNSMads
    >
    $<INSTALL_INTERFACE: 
      ${CMAKE_INSTALL_INCLUDEDIR}
    >
)

if(USE_SGTELIB MATCHES ON)
  target_sources(
    nomadAlgosStatic
    PUBLIC
      ${QUADMODEL_SOURCES} 
      ${SGTELIBMODEL_SOURCES} 
  )

  target_include_directories(
    nomadAlgosStatic
    PUBLIC
      $<BUILD_INTERFACE: 
        ${CMAKE_CURRENT_SOURCE_DIR}/Algos/QuadModel
        ${CMAKE_CURRENT_SOURCE_DIR}/Algos/SgtelibModel
      >
  )
else()
  target_include_directories(
    nomadAlgosStatic
    PRIVATE
      $<BUILD_INTERFACE: 
        $ENV{IBEX_ROOT}/src
      >
  )
endif()

if(OpenMP_CXX_FOUND)
  target_link_libraries(
    nomadAlgosStatic
    PUBLIC 
      OpenMP::OpenMP_CXX
  )
endif()

if(USE_IBEX MATCHES ON)
  target_link_libraries(
    nomadAlgosStatic
    PUBLIC 
      ibex
  )

	add_library(
    ibex 
    SHARED 
    IMPORTED
  )

	set_target_properties(
    ibex 
    PROPERTIES 
      IMPORTED_LOCATION 
        $ENV{IBEX_ROOT}/lib/libibex.so
  )
	target_include_directories(
    ibex 
    INTERFACE
      "$ENV{IBEX_ROOT}/__build__" 
  		"$ENV{IBEX_ROOT}/include/ibex"
	  	"$ENV{IBEX_ROOT}/include/ibex/3rd"
  )
	message(CHECK_START " Ready to use IBEX")
endif()

target_link_libraries(
  nomadAlgosStatic
  PUBLIC
    nomadEvalStatic
    nomadUtilsStatic
)

if(USE_SGTELIB MATCHES ON)
  target_link_libraries(
    nomadAlgosStatic
    PUBLIC 
      sgtelibStatic
  )
endif()

set_target_properties(
  nomadAlgosStatic
  PROPERTIES 
    VERSION "${NOMAD_VERSION}"
    SOVERSION "${NOMAD_VERSION_MAJOR}.${NOMAD_VERSION_MINOR}"
    OUTPUT_NAME nomadAlgosStatic
)

if(WIN32)
  target_compile_definitions(
    nomadAlgosStatic
    PRIVATE 
      -DNOMAD_BUILD_STATIC
  )
endif()

# Installing the libraries and their header files

install(
  TARGETS 
    nomadEvalStatic
    nomadAlgosStatic
    nomadUtilsStatic 
  LIBRARY DESTINATION 
    ${CMAKE_INSTALL_LIBDIR}
)

